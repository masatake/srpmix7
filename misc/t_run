#!/bin/zsh -e

S=t_src
D=_t_dest

prepare()
{
    local t=$1
    mkdir -p ./$D
    mkdir -p ./$D/$t
    rm -f ./$D/${t}.log
    rm -f ./$D/${t}.zsh.log
    rm -rf ./$D/$t/*(N)
}


run0()
{
    local src=$1
    local dest=$2

    case $src in
	(*.src.rpm)
	    SRPMIX7_XCMD_DIR=./xcmd ./srpmix7 expand --stype file --sloc $src --dtype dir --dloc $dest srpm
	    ;;
	(*.rpm)
	    SRPMIX7_XCMD_DIR=./xcmd ./srpmix7 expand --stype file --sloc $src --dtype dir --dloc $dest brpm
	    ;;
	(*.yds)
	    SRPMIX7_XCMD_DIR=./xcmd ./srpmix7 expand --stype yds --sloc $src:r:t --dtype dir --dloc $dest srpm
	    ;;
	(*)
	    echo "TEST HARNESS INTERNAL BUG: UNEXPECTED SRC FILE" 1>&2
	    exit 3
    esac
}

run()
{
    local s=${1##*/}
    local t=${s//./-}
    local v=${2}
    local log=./$D/${t}.log

    prepare "$t"


    =printf "* %45s: " ${S}/$s
    =printf "%45s -> %-45s\n" ${S}/$s ./$D/$t >> ${log}

    if run0 ${S}/$s ./$D/$t >> ${log} 2>&1; then
	if [[ -f $v ]]; then
	    if zsh $v ./$D/$t >> ${log} 2>&1; then
		tee -a ${log} <<<  "passed(expand+extra)"
		return 0
	    else
		tee -a ${log} <<< "ERROR (extra check with $v)"
	    fi
	else
	    tee -a ${log} <<< "passed(expand)"
	    return 0
	fi
    else
	case $status in
	    (1) tee -a ${log} <<< "ERROR (prepare,BUG?)"
		echo
		=printf "%40s\n" -------
		=printf "%40s\n" "DETAILS"
		=printf "%40s\n" -------
		echo
		tail -n +2 ${log} | head -n -1
		echo
		echo
		return 1;;
	    (2) tee -a ${log} <<< "error (expand)"
		return 2;;
	esac
    fi
}

main()
{
    local tmp
    local ext
    local c=0

    for ext in .rpm .yds; do
	for tmp in ${S}/*${ext}; do
	    if ! run $tmp ${tmp%${ext}}.zsh; then
		c=1
	    fi
	done
    done

    return $c
}

main
